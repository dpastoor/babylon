$PROB RUN# 1 PARALLEL (NONLINEAR+LINEAR) CLEARANCE MODEL
$INPUT C ID TIME AMT RATE DV EVID GRP WGTI
$DATA nonlinCL.csv IGNORE=C
$SUBROUTINE ADVAN6 TRANS1 TOL=8
$MODEL NPAR=10 NCOMP=2 COMP=(CENTRAL DEFOBSERVATION DEFDOSE) COMP=(PERIPH)

$PK
TVVMAX=THETA(1)*(WGTI/72)**THETA(7)
VMAX=TVVMAX*EXP(ETA(1))

KM=THETA(2)

TVCLL=THETA(3)*(WGTI/72)**THETA(8)
CLL=TVCLL*EXP(ETA(2))

TVV1=THETA(4)*(WGTI/72)**THETA(9)
V1=TVV1*EXP(ETA(3))

TVV2=THETA(5)*(WGTI/72)**THETA(10)
V2=TVV2

Q=THETA(6)

S1=V1

$DES
  C1=A(1)/V1
  C2=A(2)/V2
  DADT(1)=-VMAX*C1/(KM+C1)-(CLL*C1)-(Q*C1)+(Q*C2)
  DADT(2)=(Q*C1)-(Q*C2)

$ERROR
Y=F*EXP(EPS(1))

IPRED=F

$THETA
(0, 1.2) ;VMAX (MG/HR)
(0, 10)  ;KM (MG/L)
(0, 0.03) ;CLL (L/HR)
(0, 3)  ;V1 (L)
(0, 2)  ;V2 (L)
(0, 0.075) ;Q (L/HR)
0.8 ;WGT~VMAX
0.8 ;WGT~CLL
0.8 ;WGT~V1
0.8 ;WGT~V2

$OMEGA
0.25  ;VMAX OM
0.25  ;CLL OM
0.25  ;V1 OM

$SIGMA
0.20  ;EXPONENTIAL ERROR

;MSFI=../1.msf

$ESTIMATION METHOD=1 NOABORT MAXEVAL=9999 PRINT=10 MSFO=../1.msf
$COVARIANCE

